@name amaze the enemy to death
@inputs Target:entity
@persist Speed

if (first()) {
    
    Speed = 5 #I think this is in angles per execution
    
    function activate() {
        interval(50)
    }
    
    function angle smoothness(CurrentAngle:angle,WantedAngle:angle) {
        local WantedQuat = quat(WantedAngle)
        local CurrentQuat = quat(CurrentAngle)
        
        local LengthQ = abs(rotationAngle(WantedQuat/CurrentQuat))
        local Tick = clamp(Speed/LengthQ,0,1)
        local Quat = slerp(CurrentQuat,WantedQuat,Tick)
        
        return Quat:toAngle()
    }
    
    
    function number ang(V1:vector,V2:vector) {
        local Ret = acos(V1:dot(V2))
        if (Ret == Ret) {
            return Ret
        } else {
            return 0
        }
    }
    
    function angle smoothness2(Current:vector,Wanted:vector) {
        local CurAng = ang(Current,Wanted)
        local CurSpeed = clamp(Speed / CurAng,0,1)
        return mix(Wanted,Current,CurSpeed):toAngle()
    }
    
    if (Target) {
        activate()
    }
    
    runOnLast(1)
    holoCreate(1)
    holoScale(1,vec(0.2,0.2,1.2))
    holoParent(1,entity())
} elseif (~Target & Target) {
    activate()
} elseif (clk()) {
    local WantedAngle = (Target:pos()-holoEntity(1):pos()):toAngle()
    local CurrentAngle = holoEntity(1):angles()-ang(90,0,0) #the -ang(90,0,0) is just offset crap, you don't need to use it
    local NewAngle = smoothness(CurrentAngle,WantedAngle)
    holoAng(1,NewAngle+ang(90,0,0))
    interval(50)
} elseif (last()) {
    holoDelete(1)
}
    
