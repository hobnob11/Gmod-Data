@name Hob-Co Resource Station
@inputs 
@outputs 
@persist E:entity WL:wirelink Version DEBUG:string User:entity
@autoupdate
@model models/tiberium/dispenser.mdl
if(first())
{
    Version = 0.22
    setName("Hob-Co Resource Station | Version: " + Version)
    E = entity()
    WL = E:isWeldedTo():wirelink()
    A = 0.5
    runOnTick(1)
    
    holoCreate(1)
    holoModel(1,"hqrcylinder")
    holoPos(1,E:toWorld(vec(5,14,13)))
    holoAng(1,E:toWorld(ang()))
    holoScaleUnits(1,vec(10,10,35))
    holoAlpha(1,100)
    #holoRenderFX(1,15)
    holoMaterial(1,"models/props_combine/health_charger_glass")
    
    holoCreate(2)
    holoModel(2,"hqrcylinder")
    holoPos(2,E:toWorld(vec(5,14,13)))
    holoAng(2,E:toWorld(ang()))
    holoScaleUnits(2,vec(9,9,34))
    holoAlpha(2,200)
    holoMaterial(2,"models/alyx/emptool_glow")
    #holoRenderFX(2,15)
    holoParent(2,1)
    holoClipEnabled(2,1)
    holoClip(2,vec(0,0,-17+34*A),vec(0,0,-1),0)
    function redraw()
    {    
        WL:egpClear()
        WL:egpText(1,"Hob-Co Resource Screen",vec2(120,2))
        WL:egpFont(1,"helvetica",24)
        
        WL:egpBox(2,vec2(256,225),vec2(506,250))
        WL:egpBox(3,vec2(256,225),vec2(500,244))
        WL:egpColor(3,vec(100,100,100))
        
        WL:egpBox(4,vec2(40,10),vec2(80,20))
        WL:egpColor(4,vec(150,150,150))
        WL:egpText(5,"V: " + Version,vec2(5,0))
        WL:egpColor(5,vec(230,230,0))
        
        WL:egpBox(6,vec2(472,10),vec2(80,20))
        WL:egpColor(6,vec(150,150,150))
        WL:egpText(7,DEBUG,vec2(432,0))
        WL:egpColor(7,vec(230,230,0))
        
        findByClass("sc_resource_pump")
        findSortByDistance(E:pos())
        local Ary = findToArray()
        for(I=1,(Ary:count()>8 ? 8 : Ary:count()))
        {
            local Pmp = Ary[I,entity]
            local Owner = Pmp:owner():name():sub(0,12)
            local Clr = teamColor(Pmp:owner():team())
            local Mdl = Pmp:model():sub(Pmp:model():length() - 16,Pmp:model():length()-4)
            local Dist = round(E:pos():distance(Pmp:pos()))
            local Str = I + ": Pump: " + Mdl + " | Owner: " + Owner + " | Dist: " + Dist + "u."
            if(!Pmp:owner()){
                Str = I + ": **GLOBAL PUMP**"
            }
            local Idex = 7+I*2
            WL:egpBox(Idex,vec2(256,86+30*I),vec2(500,24))
            WL:egpColor(Idex,vec(150,150,150))
            WL:egpText(Idex+1,Str,vec2(20,76+30*I))
            WL:egpColor(Idex+1,Clr)
        }
    }
    timer(1000,0,"redraw","redraw()")
}
User = WL["User",entity]
if(changed(User) & User)
{
    DEBUG = toString(round(WL:egpCursor(User)))
}


holoPos(1,E:toWorld(vec(5,14,13)+vec(0,0,sin(curtime()*35))))
holoAng(1,E:toWorld(ang(0,curtime()*35,0)))
A = sin(curtime()*5)^2
holoClip(2,vec(0,0,-17+34*A),vec(0,0,-1),0)
holoColor(2,vec(255*(1/A),255*A,0))

