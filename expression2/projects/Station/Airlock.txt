@name Airlock
@inputs InnerButton OuterButton
@outputs InnerDoor OuterDoor DisableUse OutDoor:entity
@persist In Out
@trigger 

if(first()|dupefinished()){
    DisableUse = 1
    OutDoor = ioOutputEntities("OuterDoor")[1,entity]
    function update(){timer("UPDATE_THINGS",1)}
    holoCreate(1,entity():toWorld(vec(0,-25,-20)))
    holoModel(1,"models/ce_ls3additional/compressor/compressor_huge.mdl")
    holoAng(1,entity():angles()+ang(0,0,-90))
    holoClipEnabled(1,1,1)
    holoClip(1,1,vec(0,-21,0),vec(0,-1,0),0)
    holoClipEnabled(1,2,1)
    holoClip(1,2,vec(-50,0,0),vec(1,0,0),0)
    holoClipEnabled(1,3,1)
    holoClip(1,3,vec(50,0,0),vec(-1,0,0),0)
    holoParent(1,entity())

    emitterCreate(1,entity():pos())
    emitterSetMaterial(1,"effects/shipsplosion/smoke_003")
    emitterSetGravity(1,vec(0,0,-500))
    emitterSetVelocity(1,vec(0,0,-250))
    emitterSetRate(1,0) #20
    emitterSetLifespan(1,0.6)
    emitterSetStartSize(1,0.1)
    emitterSetEndSize(1,100)
    emitterSetVelocityNoise(1,vec(80,80,1))
    emitterSetEndAlpha(1,0)
    
    emitterCreate(2,entity():pos())
    emitterSetMaterial(2,"effects/shipsplosion/smoke_003")
    emitterSetGravity(2,vec(0,0,1000))
    emitterSetVelocity(2,vec(0,0,-250))
    emitterSetRate(2,0) #20
    emitterSetLifespan(2,0.6)
    emitterSetPos(2,entity():toWorld(vec(0,0,50)))
    emitterSetStartSize(2,50)
    emitterSetEndSize(2,0.10)
    emitterSetVelocityNoise(2,vec(80,80,1))
    emitterSetEndAlpha(2,255)
    emitterSetStartAlpha(2,0)
    
    function void out1()
    {
        InnerDoor = 0
        timer(3000,1,"out2","out2()")
        update()
    }
    function void out2()
    {
        emitterSetRate(2,20)
        entity():soundPlay("sound",0,"ambient/gas/steam_loop1.wav")
        timer(4000,1,"out3","out3()")
        update()
    }
    function void out3()
    {
        soundStop("sound")
        emitterSetRate(2,0)
        OuterDoor = 1
        timer(4000,1,"out4","out4()")
        update()
    }
    function void out4()
    {
        OuterDoor = 0
        timer(3000,1,"out5","out5()")
        update()
    }
    function void out5()
    {
        emitterSetRate(1,20)
        entity():soundPlay("sound",0,"ambient/gas/steam2.wav")
        timer(4000,1,"out6","out6()")
        update()
    }
    function void out6()
    {
        emitterSetRate(1,0)
        soundStop("sound")
        Out = 0
        update()
    }
    function void in1()
    {
        emitterSetRate(2,0)
        soundStop("sound")
        OuterDoor = 1
        timer(4000,1,"in2","in2()")
        update()
    }    
    function void in2()
    {
        OuterDoor = 0
        timer(3000,1,"in3","in3()")
        update()
    }      
    function void in3()
    {
        emitterSetRate(1,20)
        entity():soundPlay("sound",0,"ambient/gas/steam2.wav")        
        timer(4000,1,"in4","in4()")
        update()
    }  
    function void in4()
    {
        emitterSetRate(1,0)
        soundStop("sound")  
        InnerDoor = 1      
        timer(4000,1,"in5","in5()")
        update()
    }      
    function void in5()
    { 
        InnerDoor = 0      
        timer(3000,1,"in6","in6()")
        update()
    }       
    function void in6()
    { 
        In = 0
        update()
    }  
}

if(InnerButton & In == 0 & Out == 0){
    Out = 1
    InnerDoor = 1
    timer(4000,1,"out1","out1()")
}
if(OuterButton & In == 0 & Out == 0){
    In = 1
    emitterSetRate(2,20)
    entity():soundPlay("sound",0,"ambient/gas/steam_loop1.wav")
    timer(4000,1,"in1","in1()")
}
